apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

static def getLibVersionName() {
    return "0.0.4"
}

static def getLibVersionNameAsText() {
    return "v " + getLibVersionName()
}

String siteUrl = 'https://guestlogix.com'
String gitUrl = 'https://github.com/Guestlogix/traveler-android'
String libraryDescription = 'An Android SDK for Guestlogix Guest App with UI components'
String bintrayLibraryName = 'traveler_ui_kit'
String bintrayGroupId = 'com.guestlogix.traveler'
String bintrayVersion = getLibVersionName()
String bintrayRepo = 'Traveler'

version bintrayVersion
group bintrayGroupId


install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom.project {
            name bintrayLibraryName
            description libraryDescription
            url siteUrl

            packaging 'aar'
            groupId bintrayGroupId
            artifactId bintrayLibraryName
            version bintrayVersion

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            scm {
                connection gitUrl
                url siteUrl
            }
            developers {
                developer {
                    id 'Sayyam'
                    name 'Sayyam'
                }
            }
        }
    }
}


bintray {
    Properties properties = new Properties()

    if (project.rootProject.file('local.properties').canRead()) {
        properties.load(new FileInputStream(project.rootProject.file('local.properties')))
        user = properties['bintray.user'].toString()
        key = properties['bintray.api_key'].toString()

    } else {
        logger.error('Could not read local.properties. Trying Environment variables')

        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_API_KEY')
    }

    configurations = ['archives']
    dryRun = false
    pkg {
        repo = bintrayRepo
        name = bintrayLibraryName

        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        websiteUrl = siteUrl
        publish = true

        version {
            name = bintrayVersion
            released = new Date()
            vcsTag = getLibVersionNameAsText()
        }
    }
}
