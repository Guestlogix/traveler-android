apply plugin: 'com.android.library'

ext {
    androidx_version = '1.0.2'
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "${traveler_core_version}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

}

dependencies {

    //AndroidX dependencies
    implementation "com.android.support:appcompat-v7:${android_support_component_version}"

    //Testing frameworks
    testImplementation "junit:junit:${junit_version}"

    androidTestImplementation 'org.mockito:mockito-core:2.23.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'

//    androidTestImplementation 'com.android.support.test:testing-support-lib:0.1'
    androidTestImplementation ('com.android.support.test.espresso:espresso-contrib:2.0') {
        exclude module: 'support-annotations'
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            owner.classpath += variant.javaCompileProvider.get().classpath
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

repositories {
    mavenCentral()
}

apply from: 'publish.gradle'
