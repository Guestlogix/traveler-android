apply plugin: 'com.android.library'

ext {
    androidx_version = '1.0.2'
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "API_ENDPOINT", "\""+getAPIEndpoint()+"\"")
        buildConfigField("String", "TRAVELER_API_KEY", "\""+getTravelerAPIKey()+"\"")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

}

def getAPIEndpoint() {
    if (System.getenv("API_ENDPOINT") != null) {
        return System.getenv("API_ENDPOINT")
    } else {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        return properties.getProperty('api_endpoint', 'https://traveler.rc.guestlogix.io/v1')
    }
}

def getTravelerAPIKey() {
    if (System.getenv("TRAVELER_API_KEY") != null) {
        return System.getenv("TRAVELER_API_KEY")
    } else {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        return properties.getProperty('traveler_api_key', 'testtesttesttesttest')
    }
}

dependencies {

    //AndroidX dependencies
    implementation "com.android.support:appcompat-v7:${android_support_component_version}"

    //Testing frameworks
    testImplementation "junit:junit:${junit_version}"
    androidTestImplementation "androidx.test:runner:${androidx_test_runner_version}"

}
repositories {
    mavenCentral()
}
